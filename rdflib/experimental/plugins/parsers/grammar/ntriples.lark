// EBNF grammar for NTriples
start:                      statement? (EOL statement)* EOL?
statement:                  subject predicate object "."
?subject:                   IRIREF -> iriref
                            | BLANK_NODE_LABEL -> blank_node_label
?predicate:                 IRIREF -> iriref
?object:                    IRIREF -> iriref
                            | BLANK_NODE_LABEL -> blank_node_label
                            | literal
literal:                    STRING_LITERAL_QUOTE ("^^" IRIREF | LANGTAG)?

// terminals
IRIREF: "<" (/[^\x00-\x20<>"{}|^`\\]/ | UCHAR)* ">"
BLANK_NODE_LABEL: "_:" (PN_CHARS_U | "0".."9") ((PN_CHARS | ".")* PN_CHARS)?
LANGTAG: "@" /[a-zA-Z]+/ ("-" /[a-zA-Z0-9]+/)*
STRING_LITERAL_QUOTE: "\"" (/[^\x22\\\x0A\x0D]/ | ECHAR | UCHAR)* "\""
UCHAR: "\\u" HEX~4 | "\\U" HEX~8
ECHAR: "\\" /[tbnrf"'\\]/
PN_CHARS_BASE: /[A-Za-z\u00C0-\u00D6\u00D8-\u00F6\u00F8-\u02FF\u0370-\u037D\u037F-\u1FFF\u200C-\u200D\u2070-\u218F\u2C00-\u2FEF\u3001-\uD7FF\uF900-\uFDCF\uFDF0-\uFFFD\U00010000-\U000EFFFF]/
PN_CHARS_U: PN_CHARS_BASE | "_" | ":"
PN_CHARS: PN_CHARS_U | /[\-0-9\u00B7\u0300-\u036F\u203F-\u2040]/
HEX: /[0-9A-Fa-f]/
EOL: /[\r\n]/+
COMMENT: "#" /[^\n]/*

%ignore /[ \t]/+
%ignore COMMENT
